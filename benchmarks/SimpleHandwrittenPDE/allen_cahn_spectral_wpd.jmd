---
title: Allen-Cahn Pseudospectral Methods Work-Precision Diagrams
author: Arjit Seth
---


## Problem Description

The Allen-Cahn partial differential equation is solved on the domain $[-L, L] \times [0, T] \in \mathbb R \times R,~L = 1,~T = 1$, with the following initial and boundary conditions:
```math
\begin{aligned}
    \partial_t u(t,x) & = u(t,x) - u^3(t,x) + \epsilon \partial_x^2 u(t,x), \\
    u(0,x) & = \cos(2\pi x), \\
    u(t,-L) & = u(t,L) = 1.
\end{aligned}
```
The spatial derivative operators are represented via Chebyshev pseudospectral approximations. Here, the domain is discretized by projecting on a cosine-spaced grid of points $x_s \in [-1, 1]$; the solution is approximated on this grid via linear combinations of Chebyshev polynomial basis functions in space. The coefficient $p = 3,~\epsilon = 10^{-3}$ is chosen to produce `interesting' behavior as seen in the reference solution below.
```math
\begin{aligned}
    \frac{du}{dt} & = p(u(t,x) - u^3(t,x)) + \epsilon D_x^2 u(t,x),  \\
    u(0,x_s) & = \cos(2\pi x_s), \\
    u(t,-L) & = u(t,L) = 1.
\end{aligned}
```

### Implementation

```julia
using OrdinaryDiffEq
using DiffEqDevTools
using SciMLOperators
using LinearSolve
using LinearAlgebra
using SparseArrays
using ClassicalOrthogonalPolynomials
using Plots
gr();
```

```julia
# Nonlinear component of vector field
function f_nonlinear!(du, u, p, t)
    M, D0, alpha, tmp = p
    Tu = D0 * u # Pseudo-spectral representation of solution
    @. tmp[2:end-1] = alpha * (Tu - Tu^3) 
    ldiv!(du, M, tmp) # Solve the linear system M * du = tmp
end

function allen_cahn(n, eps)
    T = ChebyshevT() # Chebyshev basis
    x1 = reverse(ChebyshevGrid{1}(n - 2)) # 1st kind points, sorted
    x2 = reverse(ChebyshevGrid{2}(n)) # 2nd kind points, sorted

    V = T[x2, 1:n] # Vandermonde matrix, its inverse is transform from values to coefficients
    D0 = diff(T, 0)[x1, 1:n] / V # discretisation of identity matrix
    D2 = diff(T, 2)[x1, 1:n] / V # 2nd derivative from x2 to x1

    B_l = [1; zeros(n-1)]'  # Left Dirichlet conditions
    B_r = [zeros(n-1); 1]'  # Right Dirichlet
    M = lu([B_l; D0; B_r]) # Mass matrix for the problem
    u0 = cos.(2Ï€ * x2)  # Initial condition
    alpha = 3.0 # Time scaling factor
    D2_bc = [zeros(1, n); D2; zeros(1, n)] # 2nd derivative with Dirichlet conditions

    # Problem setup
    prob = SplitODEProblem(
        MatrixOperator(eps * (M \ D2_bc)), # Linear operator with mass matrix inversion
        f_nonlinear!,
        u0, (0.0, 1.0),
        (M, D0, alpha, similar(u0))
    )

    return x2, prob
end
```

### Reference Solution

```julia
N = 128
eps = 1e-3
xs, prob = allen_cahn(N, eps);

@time sol = solve(prob, Rodas5(autodiff=false); dt=1e-4, reltol=1e-12, abstol=1e-12);

test_sol = TestSolution(sol); # Reference solution for error estimation

tslices = LinRange(prob.tspan..., 50)
ys = mapreduce(sol, hcat, tslices)
heatmap(xs, tslices, ys', xlabel="x", ylabel="t")
```

## Work-Precision Diagrams

### Implicit-Explicit Methods

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => IMEXEuler(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNAB2(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNLF2(), :dts => 1e-4 * multipliers),
    Dict(:alg => SBDF2(), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "IMEXEuler",
    "CNAB2",
    "CNLF2",
    "SBDF2",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Work-Precision Diagram, High Tolerance")
```

### Exponential Integrators

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => NorsettEuler(), :dts => 1e-4 * multipliers),
    Dict(:alg => NorsettEuler(krylov=true, m=5), :dts => 1e-4 * multipliers),
    Dict(:alg => NorsettEuler(krylov=true, m=20), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(krylov=true, m=20), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(krylov=true, m=20), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "NorsettEuler (caching)", 
    "NorsettEuler (m=5)",
    "NorsettEuler (m=20)",
    "ETDRK2 (caching)", 
    "ETDRK2 (m=5)", 
    "ETDRK2 (m=20)"
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="ExpRK methods, High Tolerance")
```

### Explicit Methods

```julia
abstols = 0.1 .^ (5:7) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:3)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => RK4(), :dts => 1e-4 * multipliers),
    Dict(:alg => Tsit5(), :dts => 1e-4 * multipliers),
    Dict(:alg => Vern7(), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "RK4",
    "Tsit5",
    "Vern7",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Work-Precision Diagram, High Tolerance")
```


### Between Family Comparisons

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:3)
setups = [
    Dict(:alg => CNAB2(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNAB2(linsolve=KrylovJL_GMRES()), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(), :dts => 1e-4 * multipliers),
]
labels = hcat(
    "CNAB2 (dense linsolve)",
    "CNAB2 (Krylov linsolve)", 
    "ETDRK2 (m=5)",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Between family, High Tolerances")
```


```julia, echo = false
using SciMLBenchmarks
SciMLBenchmarks.bench_footer(WEAVE_ARGS[:folder],WEAVE_ARGS[:file])
```
