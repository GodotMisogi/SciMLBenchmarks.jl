---
title: Kuramoto-Sivashinsky Finite-Difference Method Work-Precision Diagrams
author: Arjit Seth
---

```julia
using OrdinaryDiffEq
using DiffEqDevTools
using SciMLOperators
using LinearSolve
using LinearAlgebra
using SparseArrays
using SummationByPartsOperators
const SBP = SummationByPartsOperators
using Plots
gr();
```

The Kuramoto-Sivashinsky equation is considered with the following initial and periodic boundary conditions:
```math
\begin{align}
    \partial_t u + \partial_x^2 u + \partial_x^4 u
    + \alpha \partial_x (u \partial_x u) & = 0 \\
    u(0,x) & = \cos\left(\frac{2\pi}{L} x\right), \quad \text{for } x \in [0, L] \\
    u(t,0) & = u(t,L) = 1
\end{align}
```
The derivatives are discretized via finite differences, incorporating the periodic boundary conditions.

```julia
nonlinear_convection!(du, u, p, t) = du .= (-p.alpha / 3) * (u .* (p.D1 * u) + p.D1 * (u .^ 2))

# Construct the problem
function kuramoto_sivashinsky(N, L, alpha)
    D1 = periodic_derivative_operator(derivative_order = 1, accuracy_order = 8,
                                      xmin = -L, xmax = L, N = N)
    D2 = periodic_derivative_operator(derivative_order = 2, accuracy_order = 8,
                                      xmin = -L, xmax = L, N = N)
    D4 = periodic_derivative_operator(derivative_order = 4, accuracy_order = 8,
                                      xmin = -L, xmax = L, N = N)

    x = SBP.grid(D1)
    u0 = @. cos(2Ï€ * x / L) # Initial condition
    p = (; D1, alpha)

    tspan = (0.0, 1.0)
    prob = SplitODEProblem(MatrixOperator(-p.alpha / 2 * (sparse(D2) + 1/4 * sparse(D4))),
                           nonlinear_convection!, 
                           u0, tspan, p);

    return x, prob
end;
```

Reference solution using an adaptive timestepping method with high precision:

```julia
N = 128  # Number of grid points
L = 16.0  # Domain length
alpha = 30.0 # Lipschitz constant scaling factor
xs, prob = kuramoto_sivashinsky(N, L, alpha)
@time sol = solve(prob, RadauIIA5(autodiff=false); dt = 1e-4, abstol=1e-14, reltol=1e-14, adaptive=true)

test_sol = TestSolution(sol);

tslices = LinRange(prob.tspan..., 50)
ys = mapreduce(sol, hcat, tslices)
plt = heatmap(xs, tslices, ys', xlabel="x", ylabel="t")
```

## Work-Precision Diagrams


### Implicit-Explicit Methods

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => IMEXEuler(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNAB2(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNLF2(), :dts => 1e-4 * multipliers),
    Dict(:alg => SBDF2(), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "IMEXEuler",
    "CNAB2",
    "CNLF2",
    "SBDF2",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Work-Precision Diagram, High Tolerance")
```

### Exponential Integrators

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => NorsettEuler(), :dts => 1e-4 * multipliers),
    Dict(:alg => NorsettEuler(krylov=true, m=5), :dts => 1e-4 * multipliers),
    Dict(:alg => NorsettEuler(krylov=true, m=20), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(krylov=true, m=20), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(krylov=true, m=20), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "NorsettEuler (caching)", 
    "NorsettEuler (m=5)",
    "NorsettEuler (m=20)",
    "ETDRK2 (caching)", 
    "ETDRK2 (m=5)", 
    "ETDRK2 (m=20)"
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="ExpRK methods, low order")
```

### Explicit Methods

```julia
abstols = 0.1 .^ (5:7) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:3)
multipliers = 0.5 .^ (0:4)
setups = [
    Dict(:alg => RK4(), :dts => 1e-4 * multipliers),
    Dict(:alg => Tsit5(), :dts => 1e-4 * multipliers),
    Dict(:alg => Vern7(), :dts => 1e-4 * multipliers)
]
labels = hcat(
    "RK4",
    "Tsit5",
    "Vern7",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Work-Precision Diagram, High Tolerance")
```


### Between Family Comparisons

```julia
abstols = 0.1 .^ (5:8) # all fixed dt methods so these don't matter much
reltols = 0.1 .^ (1:4)
multipliers = 0.5 .^ (0:3)
setups = [
    Dict(:alg => CNAB2(), :dts => 1e-4 * multipliers),
    Dict(:alg => CNAB2(linsolve=KrylovJL_GMRES()), :dts => 1e-4 * multipliers),
    Dict(:alg => ETDRK2(), :dts => 1e-4 * multipliers),
]
labels = hcat(
    "CNAB2 (dense linsolve)",
    "CNAB2 (Krylov linsolve)", 
    "ETDRK2 (caching)",
)
@time wp = WorkPrecisionSet(prob, abstols, reltols, setups;
    print_names=true, names=labels, numruns=5, error_estimate=:l2,
    save_everystep=false, appxsol=test_sol, maxiters=Int(1e5));

plot(wp, label=labels, markershape=:auto, title="Between family, low orders")
```


```julia, echo = false
using SciMLBenchmarks
SciMLBenchmarks.bench_footer(WEAVE_ARGS[:folder],WEAVE_ARGS[:file])
```
